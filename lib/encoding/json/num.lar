//json num对象

/*
to_json_num_value将构造JsonNum时输入的参数转为标准的json num value
整数类型依具体值转为long或ulong类型
浮点数转double类型
其他值抛ValueError异常
*/
native Any to_json_num_value(Any v);

public class JsonNum
{
    public JsonNum(Any v)
    {
        v = to_json_num_value(v);
        this.base = new JsonBase(this);
        this.v = v;
    }

    public String type()
    {
        return "num";
    }

    public char[] marshal()
    {
        return marshal_num(this.v);
    }

    public long as_long()
    {
        ulong n;
        if (try_cast<ulong>(this.v, ref n) && n < (1UL << 63))
        {
            return (long)n;
        }
        return (long)this.v;
    }

    public ulong as_ulong()
    {
        long n;
        if (try_cast<long>(this.v, ref n) && n >= 0)
        {
            return (ulong)n;
        }
        return (ulong)this.v;
    }

    public double as_double()
    {
        long n;
        if (try_cast<long>(this.v, ref n))
        {
            return (double)n;
        }
        ulong un;
        if (try_cast<ulong>(this.v, ref un))
        {
            return (double)un;
        }
        return (double)this.v;
    }

    JsonBase base usemethod;
    Any v;
}
